/*
 * Copyright 2000-2020 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'com.github.rodm.teamcity-server' version "1.0"
  id 'com.ullink.nuget' version '2.15'
}

teamcity {
  version = teamcityVersion
  server {
    descriptor = project.file('teamcity-plugin.xml')
    tokens = [Plugin_Version: project.version]
    files {
      into('server') {
        from('tools')
      }

      into('kotlin-dsl') {
        from("${rootProject.projectDir}/kotlin-dsl")
      }
    }
  }
}

dependencies {
  compile project(':plugin-dotnet-common')
  compile 'com.google.code.gson:gson:2.5'
  compile "org.jetbrains.kotlin:kotlin-stdlib"

  provided files("${teamcityDir}/webapps/ROOT/WEB-INF/lib/server-tools.jar")
  provided 'org.jetbrains.teamcity:server-api:${teamcityVersion}'
  testCompile 'io.mockk:mockk:1.9.3'
  agent project(path: ':plugin-dotnet-agent', configuration: 'plugin')
}

nugetRestore {
  packagesConfigFile = file('./../packages.config')
  packagesDirectory = './packages'
}

project.tasks.nugetRestore.doFirst {
  println 'Clean .NET integration directories'
  ant.delete(dir: './packages')
  ant.delete(dir: './tools')
}

project.tasks.nugetRestore.doLast {
  def path = ant.path {
    fileset(dir: './packages', includes: 'TeamCity.Dotnet.Integration.*/TeamCity.Dotnet.Integration.*.nupkg')
  }

  def nuSpecFile = path.list().first()
  def nuSpecFileName = file(nuSpecFile).name

  println 'Use .NET integration nuSpec file: ' + nuSpecFileName

  nuSpecFileName = nuSpecFileName.replaceAll('.nupkg', '')

  ant.unzip(src: nuSpecFile, dest: './packages', overwrite: 'true') {
    patternset() {
      include(name: 'TeamCity.Dotnet.Integration.nuspec')
    }
  }

  ant.zip(baseDir: './packages', includesfile: 'packages/TeamCity.Dotnet.Integration.nuspec', destfile: './tools/' + nuSpecFileName + '.jar') {
    patternset() {
      include(name: 'TeamCity.Dotnet.Integration.nuspec')
    }
  }

  path = ant.path {
    fileset(dir: './tools', includes: '**/*')
  }

  path.list().each { println it }

  ant.delete(dir: './packages')
}

serverPlugin.dependsOn(nugetRestore)

serverPlugin.version = null
serverPlugin.baseName = projectIds.artifact

sourceCompatibility = 1.7
targetCompatibility = 1.7